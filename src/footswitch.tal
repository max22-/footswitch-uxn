( Uxn macros )
%RTN { JMP2r }

( Arduino stuff )
%INPUT { #00 }
%OUTPUT { #01 }
%INPUT_PULLUP { #02 }
%INPUT_PULLDOWN { #03 }
%INPUT_ANALOG { #04 }
%LOW { #00 }
%HIGH { #01 }
%PINMODE { .Pin/number DEO .Pin/mode DEO } ( mode pinNumber -- )
%DIGITALWRITE { .Pin/number DEO .Pin/digitalWrite DEO } ( value pinNumber -- ) 
%DIGITALREAD { .Pin/number DEO .Pin/digitalRead DEI } ( pinNumber -- value )

( Bluepill stuff )
%PC13 { #11 }
%PB9 { #00 }
%PB8 { #01 }
%PB7 { #02 }
%PB6 { #03 }
%PB5 { #04 }
%PB4 { #05 }

( Config )
%LED { PC13 }		( PC13 = pin number 17 --> #11 in hex )
%BUTTON1 { PB9 }
%BUTTON2 { PB8 }
%BUTTON3 { PB7 }
%BUTTON4 { PB6 }
%BUTTON5 { PB5 }
%BUTTON6 { PB4 }
%DEBOUNCE-DELAY { #0014 } ( 20 ms )

( Devices )

|10 @Console    [ &vector $2 &read     $1 &pad    	$5 &write	$1 ]
|c0 @Pin	    [ &vector $2 &number   $1 &mode 	$1 &digitalRead	$1 &digitalWrite 	$1 ]
|d0 @Clock		[ &vector $2 &millis $2 ]


|0100 ( -> )
       
 	;setup JSR2
	;loop .Clock/vector DEO2

BRK

@setup
	OUTPUT LED PINMODE
	INPUT_PULLUP BUTTON1 PINMODE
	INPUT_PULLUP BUTTON2 PINMODE
	INPUT_PULLUP BUTTON3 PINMODE
	INPUT_PULLUP BUTTON4 PINMODE
	INPUT_PULLUP BUTTON5 PINMODE
	INPUT_PULLUP BUTTON6 PINMODE
RTN

@loop

	;callback1 BUTTON1 ;read-button JSR2
	;callback2 BUTTON2 ;read-button JSR2

BRK

@read-button ( callback pinNumber -- )
    
	DUP DIGITALREAD LOW EQU ,&call JCN
        POP POP2 ,&end JMP ( callback pinNumber -- )
        &call
        ROT ROT ( callback pinNumber -- pinNumber callback )
        JSR2 ( pinNumber callback -- pinNumber )

	( debounce )
	DEBOUNCE-DELAY ;delay JSR2

	&waitRelease ( pinNumber -- pinNumber )

    DUP (pinNumber -- pinNumber pinNumber )
	DIGITALREAD LOW EQU ,&waitRelease JCN ( pinNumber pinNumber -- pinNumber )

    POP ( pinNumber -- )
	DEBOUNCE-DELAY ;delay JSR2

    &end
RTN

@callback1
	#41 .Console/write DEO #0a .Console/write DEO
RTN

@callback2
	#42 .Console/write DEO #0a .Console/write DEO
RTN

@delay ( ms -- )
	.Clock/millis DEI2 SWP2
	&clockLoop
	OVR2 .Clock/millis DEI2 SWP2 SUB2 OVR2 LTH2 ,&clockLoop JCN
	POP2 POP2
RTN
