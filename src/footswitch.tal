( Uxn macros )
%RTN { JMP2r }

( Arduino stuff )
%INPUT { #00 }
%OUTPUT { #01 }
%INPUT_PULLUP { #02 }
%INPUT_PULLDOWN { #03 }
%INPUT_ANALOG { #04 }
%LOW { #00 }
%HIGH { #01 }
%PINMODE { .Pin/number DEO .Pin/mode DEO } ( mode pinNumber -- )
%DIGITALWRITE { .Pin/number DEO .Pin/digitalWrite DEO } ( value pinNumber -- ) 
%DIGITALREAD { .Pin/number DEO .Pin/digitalRead DEI } ( pinNumber -- value )

( Bluepill stuff )
%PC13 { #11 }
%PB9 { #00 }
%PB8 { #01 }
%PB7 { #02 }
%PB6 { #03 }
%PB5 { #04 }
%PB4 { #05 }

( Midi )
%PC { #c0 ADD .Console/write DEO .Console/write DEO } ( channel program -- )

( Config )
%LED { PC13 }
%BUTTON0 { PB9 }
%BUTTON1 { PB8 }
%BUTTON2 { PB7 }
%BUTTON3 { PB6 }
%BUTTON4 { PB5 }
%BUTTON5 { PB4 }
%DEBOUNCE-DELAY { #0014 } ( 20 ms )

( Devices )

|10 @Console    [ &vector $2 &read     $1 &pad    	$5 &write	$1 ]
|c0 @Pin	    [ &vector $2 &number   $1 &mode 	$1 &digitalRead	$1 &digitalWrite 	$1 ]
|d0 @Clock		[ &vector $2 &millis $2 ]


|0100 ( -> )
       
 	;setup JSR2
	;loop .Clock/vector DEO2

BRK

@setup
	OUTPUT LED PINMODE
	INPUT_PULLUP BUTTON0 PINMODE
	INPUT_PULLUP BUTTON1 PINMODE
	INPUT_PULLUP BUTTON2 PINMODE
	INPUT_PULLUP BUTTON3 PINMODE
	INPUT_PULLUP BUTTON4 PINMODE
	INPUT_PULLUP BUTTON5 PINMODE
RTN

@loop
	;callback0 BUTTON0 ;read-button JSR2
	;callback1 BUTTON1 ;read-button JSR2
	;callback2 BUTTON2 ;read-button JSR2
	;callback3 BUTTON3 ;read-button JSR2
	;callback4 BUTTON4 ;read-button JSR2
	;callback5 BUTTON5 ;read-button JSR2
BRK


@read-button ( callback pinNumber -- )
    
	DUP DIGITALREAD LOW EQU ,&call JCN
	POP POP2 ,&end JMP ( callback pinNumber -- )
	&call
	ROT ROT ( callback pinNumber -- pinNumber callback )
	JSR2 ( pinNumber callback -- pinNumber )

	( debounce )
	DEBOUNCE-DELAY ;delay JSR2

	&waitRelease ( pinNumber -- pinNumber )

    DUP (pinNumber -- pinNumber pinNumber )
	DIGITALREAD LOW EQU ,&waitRelease JCN ( pinNumber pinNumber -- pinNumber )

    POP ( pinNumber -- )
	DEBOUNCE-DELAY ;delay JSR2

    &end

RTN


@callback0
	#00 #00 PC	( program change, channel 0, program 0 )
RTN

@callback1
	#00 #01 PC
RTN

@callback2
	#00 #02 PC
RTN

@callback3
	#00 #03 PC
RTN

@callback4
	#00 #04 PC
RTN

@callback5
	#00 #05 PC
RTN

@delay ( ms -- )
	.Clock/millis DEI2 SWP2
	&clockLoop
	OVR2 .Clock/millis DEI2 SWP2 SUB2 OVR2 LTH2 ,&clockLoop JCN
	POP2 POP2
RTN
